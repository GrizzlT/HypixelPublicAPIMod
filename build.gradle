// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

version = "2.0.0"
group = "com.github.grizzlt.hypixel-public-api" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "hypixel-public-api-modapi"

// Use JDK 8 for compilation
sourceCompatibility = targetCompatibility = 1.8

// Use UTF-8 for encoding
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = "1.8.9-11.15.1.1855"
    runDir = "run"
    mappings = "stable_20"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

configurations {
    shade
    compile.extendsFrom(shade)
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}

// use this in dependencies to shadow the right libs
//noinspection GroovyMissingReturnStatement
/*reobf {
    jar {
        extraLines "PK: reactor com/github/grizzlt/hypixelpublicapi/shadowedLibs/reactor"
        extraLines "PK: net/hypixel/api com/github/grizzlt/hypixelpublicapi/shadowedLibs/net/hypixel/api"
        extraLines "PK: org/reactivestreams com/github/grizzlt/hypixelpublicapi/shadowedLibs/org/reactivestreams"
        extraLines "PK: io/netty com/github/grizzlt/hypixelpublicapi/shadowedLibs/io/netty"
    }
}*/

dependencies {
    compile ('com.github.grizzlt.hypixel:hypixel-api-core:4.0.0-SNAPSHOT') {
        exclude module: 'gson'
        exclude module: 'guava'
        exclude module: 'commons-io'
    }
}

jar {
    dependsOn configurations.compile
    from {
        configurations.shade.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    exclude 'META-INF/**'
}

publishing {
    publications {
        hypixelmodapi(MavenPublication) {
            from components.java
            // more goes in here

            artifactId project.archivesBaseName

            pom.withXml {
                Node pomNode = asNode()
                pomNode.dependencies.'*'.findAll() {
                            (it.artifactId.text() == 'hypixel-api-core')
                }.each() {
                    it.remove(it.scope)
                }
            }
        }
    }
}