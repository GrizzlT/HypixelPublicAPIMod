
// For those who want the bleeding edge
buildscript {
    repositories {
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}
plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id "com.dorongold.task-tree" version "1.5"
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

version = "2.2.0"
group= "com.github.grizzlt.hypixelapimod" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "HypixelPublicAPIMod"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
// Use UTF-8 for encoding
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = "1.8.9-11.15.1.1855"
    runDir = "run"
    mappings = "stable_20"
}

repositories {
    maven { url 'https://repo.hypixel.net/repository/Hypixel/' }
    maven { url 'https://jitpack.io' }
}

sourceSets {
    api
    main {
        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
    }
}

dependencies {
    apiCompile 'net.hypixel:hypixel-api-core:4.0'
    compile ('net.hypixel:hypixel-api-core:4.0') {
        exclude module: 'gson'
    }

    compile ('com.github.GrizzlT.PublicAPI:hypixel-api-transport-reactor:bugfix~reactor_concurrency-SNAPSHOT') {
        exclude module: 'hypixel-api-core'
    }
}

task apiJar(type: Jar) {
    description = "Assembles api sourceSet"
    group = "build"
    appendix = "api"
    from sourceSets.api.output
}

shadowJar {
    from sourceSets.api.output.classesDir
    classifier = ''
    /*from {
        configurations.shade.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }*/
    //exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/INDEX.LIST', 'META-INF/**'
}

reobf {
    shadowJar {
        extraLines "PK: reactor com/github/grizzlt/hypixelapimod/shadowedLibs/reactor"
        extraLines "PK: org/reactivestreams com/github/grizzlt/hypixelapimod/shadowedLibs/org/reactivestreams"
        extraLines "PK: io/netty com/github/grizzlt/hypixelapimod/shadowedLibs/io/netty"
    }
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task apiJavadoc(type: Javadoc) {
    classpath = sourceSets.api.output + sourceSets.api.compileClasspath
    source = sourceSets.api.allJava
}

task packageApiJavadoc(type: Jar, dependsOn: 'apiJavadoc') {
    from apiJavadoc.destinationDir
    classifier = 'api-javadoc'
}

task packageApiSources(type: Jar, dependsOn: 'apiClasses') {
    from sourceSets.api.allSource
    classifier = 'api-sources'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'hypixelapimod'

            artifact apiJar
            artifact packageApiJavadoc
            artifact packageApiSources
        }
    }
}
